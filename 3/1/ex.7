(define (make-account balance password)
  (define (withdraw amount)
    (if (>= balance amount)
        (begin (set! balance
                     (- balance amount))
               balance)
        "Insufficient funds"))
  (define (deposit amount)
    (set! balance (+ balance amount))
    balance)
  (define (add-password phrase)
    (set! password (cons phrase password))
    dispatch)
  (define (dispatch key m)
    (if (memq key password)
        (cond ((eq? m 'withdraw) withdraw)
              ((eq? m 'deposit) deposit)
              ((eq? m 'add-password) add-password)
              (else (error "Unknown request:
                     MAKE-ACCOUNT" m)))
        (lambda (x) "Incorrect password")))
  (set! password (list password))
  dispatch)

(define (make-joint acc password new-password)
  ((acc password 'add-password) new-password))
