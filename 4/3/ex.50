selectors:
(define (ramb? exp) (tagged-list? exp 'ramb))
(define (ramb-choices exp) (cdr exp))

to ambeval:
((ramb? exp) (analyze-ramb exp))



(define (analyze-ramb exp)
  (define (cut-nth-elem list n)
    (define (iter before after n)
      (if (= n 0)
          (append (reverse before)
                  (cdr after))
          (iter (cons (car after)
                      before)
                (cdr after)
                (- n 1))))
    (iter '() list n))
  (let ((cprocs
         (map analyze (ramb-choices exp))))
    (lambda (env succeed fail)
      (define (try-next choices)
        (if (null? choices)
            (fail)
            (let ((random-element (random (length choices))))
              ((list-ref choices random-element)
               env
               succeed
               (lambda ()
                 (try-next (cut-nth-elem choices random-element)))))))
      (try-next cprocs))))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(define (parse-word word-list)
  (define (choose-word words)
    (require (not (null? words)))
    (ramb (car words)
         (choose-word (cdr words))))
  (let ((words (cdr word-list))
        (word-type (car word-list)))
    (list word-type (choose-word words)))) ; the resulting word may be parsed or unparsed, it is up to the implementation

(define (parse-noun-phrase)
  (define (maybe-extend noun-phrase)
    (ramb
     noun-phrase
     (maybe-extend
      (list 'noun+prep-phrase
            noun-phrase
            (parse-prepositional-phrase)))))
  (list 'noun-phrase
        (parse-word articles)
        (maybe-extend (parse-adjective-noun-phrase))))

(define (parse-adjective-noun-phrase)
  (define (surely-extend adjective)
    (list 'adjective-noun-phrase
          adjective
          (ramb (parse-word nouns)
               (surely-extend (parse-word adjectives)))))
  (ramb (parse-word nouns)
       (surely-extend (parse-word adjectives))))

; one can unite ajective so that adjective-noun-phrase is used only before the first adjective.
; this may be achieved by using cons
; the same relates to adverbs
(define (parse-adjective-noun-phrase)
  (define (recurse adjective)
    (cons adjective
          (ramb (recurse (parse-word adjectives))
               (list (parse-word nouns)))))
  (ramb (parse-word nouns)
       (cons 'adjective-noun-phrase
             (recurse (parse-word adjectives)))))

(define adverbs
  '(adverb fully strongly hardly lately lazily hastily slowly often rarely))

(define adjectives
  '(adjective beautiful ugly tasty funny stinky good nice))

;(define (parse-simple-noun-phrase)
;  (list 'simple-noun-phrase
;        (parse-word articles)
;        (parse-word nouns)))

;somehow I do not want to have a grammar where more than 1 adverb is possible before a single verb.
; There is a bug that several layers of 'verb-phrase may be attached to the phrase
; I know no workaround
(define (parse-verb-phrase)
  (define (maybe-extend verb-phrase)
    (ramb
     verb-phrase
     (maybe-extend
      (list 'verb-phrase
            verb-phrase
            (parse-prepositional-phrase)))))
  (maybe-extend (parse-adverb-verb-phrase)))

(define (parse-adverb-verb-phrase)
  (ramb (parse-word verbs)
       (list 'adverb-verb-phrase
             (parse-word adverbs)
             (parse-word verbs))))
;----------------
;Experimental
(define (parse-sentence)
  (define (maybe-compound sentence)
    (ramb sentence
         (list 'compound-sentence
               sentence
               (parse-word conjunctions)
               (parse-sentence))))
  (maybe-compound
         (list 'sentence
               (parse-noun-phrase)
               (parse-verb-phrase))))

(define conjunctions
  '(conjunction and or but so))
